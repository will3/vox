// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

float _HillHeight;
float _PlainHeight;
float4 _RockColor;

StructuredBuffer<float> _CanyonBuffer;
StructuredBuffer<float> _HeightBuffer;
RWStructuredBuffer<float> _VoxelBuffer;
RWStructuredBuffer<float4> _ColorBuffer;
int _Size;
float3 _Origin;

int getIndex(int3 coord) {
    int dataSize = _Size + 3;
    return (coord.x * dataSize * dataSize) + (coord.y * dataSize) + coord.z;
}

[numthreads(8, 8, 8)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    int dataSize = _Size + 3;
    if (id.x >= dataSize || id.y >= dataSize || id.z >= dataSize) {
        return;
    }

    int3 coord = int3(id.x, id.y, id.z);
    int index = getIndex(coord);

    float biome = _CanyonBuffer[index];
    float terrainHeight;

    if (biome < 0.1 && biome > -0.1)
    {
        float ratio = (biome + 0.1) / 0.2;
        terrainHeight = _PlainHeight + (_HillHeight - _PlainHeight) * ratio;
    }
    else if (biome > 0)
    {
        terrainHeight = _HillHeight;
    }
    else
    {
        terrainHeight = _PlainHeight;
    }

    float absY = coord.y + _Origin.y;
    float gradient = (1.0 - (absY / terrainHeight)) - 0.5;
    float height = _HeightBuffer[index];
    float value = height + gradient;

    _VoxelBuffer[index] = value;
    _ColorBuffer[index] = _RockColor;
}
